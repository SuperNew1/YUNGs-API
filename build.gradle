buildscript {
    repositories {
        // These repositories are only for Gradle plugins, put any other repositories in the repository block further below
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'
apply plugin: 'signing'

// Maven info
version = "${modMinecraftVersion}-${modBuildNum}"
group = modGroup
archivesBaseName = modFileName

// Mojang ships Java 16 to end users in 1.17+ instead of Java 8 in 1.16 or lower, so your mod should target Java 16.
java.toolchain.languageVersion = JavaLanguageVersion.of(16)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: modMcpMappingsChannel, version: modMcpMappingsVersion
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
//            arg "-mixin.config=" + modId + ".mixins.json"
//            property 'mixin.env.disableRefMap', 'true'
//            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
//            property 'forge.logging.console.level', 'debug'

            mods { yungsapi { source sourceSets.main } }
        }

        server {
            workingDirectory project.file('run')
//            arg "-mixin.config=" + modId + ".mixins.json"
//            property 'mixin.env.disableRefMap', 'true'
//            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
//            property 'forge.logging.console.level', 'debug'

            mods { yungsapi { source sourceSets.main } }
        }

        data {
            workingDirectory project.file('run')
//            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
//            property 'forge.logging.console.level', 'debug'

            args '--mod', 'yungsapi', '--all', '--output', file('src/generated/resources/')

            mods { yungsapi { source sourceSets.main } }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:" + modMinecraftVersion + "-" + modForgeVersion
    implementation 'com.google.code.gson:gson:2.8.6'
//    if (System.getProperty("idea.sync.active") != "true") {
//        annotationProcessor 'org.spongepowered:mixin:0.8.2:processor'
//    }
}

processResources {
    duplicatesStrategy = 'include'

    // this will ensure that this task is redone when any variables change
    inputs.property "modId", modId
    inputs.property "version", project.version
    inputs.property "modName", modName
    inputs.property "modCredits", modCredits
    inputs.property "modAuthor", modAuthor
    inputs.property "modDescription", modDescription
    inputs.property "modMinecraftVersion", modMinecraftVersion

    // Replace stuff in mods.toml, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        // Replace vars in mods.toml
        expand 'modId':modId,
                'version': project.version,
                'modName':modName,
                'modCredits':modCredits,
                'modAuthor':modAuthor,
                'modDescription':modDescription,
                'modMinecraftVersion': modMinecraftVersion
    }

    // Copy everything else except the mods.toml
    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
                "Specification-Title"     : modId,
                "Specification-Vendor"    : modGroup,
                "Specification-Version"   : "1.0", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : modGroup,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Signing-Fingerprint"     : project.hasProperty('signSHA1') ? project.findProperty('signSHA1') : "unsigned"
//                "MixinConfigs": "${modId}.mixins.json"
        ],)
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

// Example configuration to allow publishing using the maven-publish task
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar')
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
//publish.dependsOn('reobfJar')

// Note that the ossrhUsername and ossrhPassword, as well as the
// signing.keyId, signing.password, and signing.secretKeyRingFile variables are defined separately
// in my global gradle.properties for protection
signing {
    sign configurations.archives
}

publishing {
    publications  {
        mavenJava(MavenPublication) {
            artifactId = "YungsApi"
            // We avoid using 'from components.java', and instead specify artifacts individually,
            // Since we want to use the output of remapJar rather than the regular jar
            artifact jar
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = "YUNG's API"
                description = "A common API for YUNG's Minecraft mods"
                url = 'https://github.com/yungnickyoung/YUNGs-Api'
                scm {
                    url = 'https://github.com/yungnickyoung/YUNGs-Api'
                    connection = 'scm:git:git://github.com/yungnickyoung/YUNGs-Api.git'
                    developerConnection = 'scm:git:ssh://git@github.com:yungnickyoung/YUNGs-Api.git'
                }
                licenses {
                    license {
                        name = 'GNU GPLv3'
                        url = 'https://github.com/yungnickyoung/YUNGs-Api/blob/fabric-1.17/LICENSE'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        name = 'YUNGNICKYOUNG'
                        email = 'yungnickyoung@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

// signing.keyId, signing.password, and signing.secretKeyRingFile are defined separately
// in my global gradle.properties for protection
signing {
    sign publishing.publications.mavenJava
}